apply plugin: 'com.android.application'

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
    if (project.hasProperty("pvTestingAccessKey")) {
        properties.put("pvTestingAccessKey", project.getProperty("pvTestingAccessKey"))
    }
    if (project.hasProperty("numTestIterations")) {
        properties.put("numTestIterations", project.getProperty("numTestIterations"))
    }
    if (project.hasProperty("performanceThresholdSec")) {
        properties.put("performanceThresholdSec", project.getProperty("performanceThresholdSec"))
    }
    if (project.hasProperty("pvTestingDevice")) {
        properties.put("pvTestingDevice", project.getProperty("pvTestingDevice"))
    }

    if (project.hasProperty("storePassword")) {
        properties.put("storePassword", project.getProperty("storePassword"))
    }
    if (project.hasProperty("storeFile")) {
        properties.put("storeFile", project.getProperty("storeFile"))
    }
    if (project.hasProperty("keyAlias")) {
        properties.put("keyAlias", project.getProperty("keyAlias"))
    }
    if (project.hasProperty("keyPassword")) {
        properties.put("keyPassword", project.getProperty("keyPassword"))
    }
}

android {
    compileSdk defaultTargetSdkVersion

    defaultConfig {
        applicationId "ai.picovoice.koala.testapp"
        minSdkVersion 21
        targetSdkVersion defaultTargetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue 'string', 'pvTestingAccessKey', properties.getProperty("pvTestingAccessKey", "")
        resValue 'string', 'numTestIterations', properties.getProperty("numTestIterations", "")
        resValue 'string', 'performanceThresholdSec', properties.getProperty("performanceThresholdSec", "")
        resValue 'string', 'device', properties.getProperty("device", "")
    }

    signingConfigs {
        release {
            storePassword properties.getProperty("storePassword")
            storeFile file(properties.getProperty("storeFile", ".dummy.jks"))
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    if (System.getProperty("testBuildType", "debug") == "integ") {
        testBuildType("release")
    }

    sourceSets {
        androidTest {
            java {
                if (System.getProperty("testBuildType", "debug") == "perf") {
                    exclude "**/KoalaTest.java"
                    exclude "**/IntegrationTest.java"
                } else if (System.getProperty("testBuildType", "debug") == "integ") {
                    exclude "**/KoalaTest.java"
                    exclude "**/PerformanceTest.java"
                } else {
                    exclude "**/IntegrationTest.java"
                    exclude "**/PerformanceTest.java"
                }
            }
        }
    }

    task("copyParams", type: Copy) {
        from("$projectDir/../../../../lib/common/")
        include("koala_params.pv")
        into("$projectDir/src/main/assets/models")
    }
    task("copyAudio", type: Copy) {
        description = "Copy audio resources"
        from("$projectDir/../../../../resources/audio_samples/")
        include("noise.wav")
        into("$projectDir/src/main/assets/audio_samples")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        abortOnError false
    }
    namespace 'ai.picovoice.koala.testapp'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation files('../../Koala/koala/build/outputs/aar/koala-release.aar')

    // Espresso UI Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('androidx.test.espresso:espresso-intents:3.5.1')
}

afterEvaluate {
        tasks."mergeDebugAssets".dependsOn "copyParams"
        tasks."mergeReleaseAssets".dependsOn "copyParams"
        tasks."generateReleaseLintVitalReportModel".dependsOn "copyParams"
        tasks."lintVitalAnalyzeRelease".dependsOn "copyParams"
        tasks."mergeDebugAssets".dependsOn "copyAudio"
        tasks."mergeReleaseAssets".dependsOn "copyAudio"
        tasks."generateReleaseLintVitalReportModel".dependsOn "copyAudio"
        tasks."lintVitalAnalyzeRelease".dependsOn "copyAudio"
}
